(*  OpcUaClient --[CreateSessionRequest]--> SessionEndpoint
    SessionEndpoint --[Ns2]--> OpcUaClient
    OpcUaClient --[pkCli, Login, Password]--> SessionEndpoint
    SessionEndpoint --[ActivateSessionResponse]--> OpcUaClient
*)

free c: channel.


(* Hosts *)
type host.
free A, B: host.


(* Hash function *)
fun h(bitstring): bitstring.


(* Symetric key crypto *)
fun senc(bitstring, bitstring): bitstring.
reduc forall x: bitstring, k: bitstring ; sdec(senc(x, k), k) = x.


(* Signatures *)
fun sk(host): bitstring.
fun pub(bitstring): bitstring.
fun sign(bitstring, bitstring): bitstring.
reduc forall x:bitstring, s:bitstring, k:bitstring ; verify(x, sign(x, k), pub(k)) = true.


(* Constants *)
const CreateSessionRequest: bitstring.
const CreateSessionResponse: bitstring.
const ActivateSessionRequest: bitstring.
const ActivateSessionResponse: bitstring.


(* Private keys *)
free skA: bitstring [private].
free skB: bitstring [private].
fun Skey(bitstring, bitstring): bitstring [private].


(* Secrets *)
fun Login(bitstring): bitstring [private].
fun Passwd(bitstring): bitstring [private].


(* Events *)
event beginCli(host, host, bitstring).
event endSrv(host, host, bitstring).
event beginSrv(host, host, bitstring).
event endCli(host, host, bitstring).


(* Goals *)
query attacker(Login(skA)).
query attacker(Passwd(skA)).

query X: host, Y: host, N: bitstring ; event(endSrv(X, Y, N)) ==> event(beginCli(X, Y, N)).
query X: host, Y: host, N: bitstring ; event(endCli(X, Y, N)) ==> event(beginSrv(X, Y, N)).


let client(skA: bitstring, skB: bitstring, ch: channel) = 
    in(c, (Cli: host, Sess: host));
    let skCli = if Cli = A then skA else if Cli = B then skB in
    let pkCli = pub(skCli) in
    let skSess = if Sess = A then skA else if Sess = B then skB else sk(Sess) in
    let pkSess = pub(skSess) in
    new K: bitstring;
    out(ch, (Cli, Sess, K));
    let tmp = if Sess <> A && Sess <> B then K else CreateSessionRequest in out(c, tmp);

    (* Role starting *)
    new Nc: bitstring;
    let m1 = (CreateSessionRequest, pkCli, Nc) in
    out(c, (senc(m1, K), sign(h(m1), skCli)));

    in(c, (em2: bitstring, sigM2: bitstring));
    let m2 = sdec(em2, K) in
    if verify(h(m2), sigM2, pkSess) <> true then 0 else
    let (=CreateSessionResponse, =Cli, =pkSess, Ns: bitstring) = m2 in

    event beginCli(Cli, Sess, (Login(skCli), Passwd(skCli)));
    let m3 = (ActivateSessionRequest, Sess, pkCli, Login(skCli), Passwd(skCli)) in
    out(c, (senc(m3, K), sign(h(m3), skCli)));

    in(c, (em4: bitstring, sigM4: bitstring));
    let m4 = sdec(em4, K) in
    if verify(h(m4), sigM4, pkSess) <> true then 0 else
    let (=ActivateSessionResponse, Ns2: bitstring) = m4 in
    if Sess = A || Sess = B then
    event endCli(Sess, Cli, Ns).


let sess(skA: bitstring, skB: bitstring, ch: channel) = 
    in(ch, (Cli: host, Sess: host, K: bitstring));
    let skCli = if Cli = A then skA else if Cli = B then skB else sk(Cli) in
    let pkCli = pub(skCli) in
    let skSess = if Sess = A then skA else if Sess = B then skB in
    let pkSess = pub(skSess) in

    (* Role starting *)
    in(c, (m1: bitstring, sigM1: bitstring));
    if verify(h(m1), sigM1, pkCli) <> true then 0 else
    let (=CreateSessionRequest, =pkCli, Nc: bitstring) = m1 in

    new Ns: bitstring;
    event beginSrv(Sess, Cli, Ns);
    let m2 = (CreateSessionResponse, Cli, pkSess, Ns) in
    out(c, (m2, sign(h(m2), skSess)));

    in(c, (m3: bitstring, sigM3: bitstring));
    if verify(h(m3), sigM3, pkCli) <> true then 0 else
    let (=ActivateSessionRequest, =Sess, =pkCli, =Login(skCli), =Passwd(skCli)) = m3 in
    
    new Ns2: bitstring;
    let m4 = (ActivateSessionResponse, Ns2) in
    out(c, (m4, sign(h(m4), skSess)));
    if Cli = A || Cli = B then
    event endSrv(Cli, Sess, (Login(skCli), Passwd(skCli))).


process
    let pkA = pub(skA) in out(c, pkA);
    let pkB = pub(skB) in out(c, pkB);

    !(new ch: channel;
      client(skA, skB, ch)  |
      sess(skA, skB, ch)
    )
