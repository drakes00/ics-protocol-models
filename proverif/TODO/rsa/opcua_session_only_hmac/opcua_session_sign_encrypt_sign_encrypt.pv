(*  OpcUaClient --[CreateSessionRequest]--> SessionEndpoint
    SessionEndpoint --[Ns2]--> OpcUaClient
    OpcUaClient --[pkCli, Login, Password]--> SessionEndpoint
    SessionEndpoint --[ActivateSessionResponse]--> OpcUaClient
*)

free c: channel.


(* Hosts *)
type host.
free A, B: host.


(* Hash function *)
fun h(bitstring): bitstring.


(* Symetric key crypto *)
fun senc(bitstring, bitstring): bitstring.
reduc forall x: bitstring, k: bitstring ; sdec(senc(x, k), k) = x.


(* Signatures *)
fun pub(bitstring): bitstring.
fun sk(host): bitstring.
fun hmac(bitstring, bitstring): bitstring.
reduc forall x:bitstring, s:bitstring, k:bitstring ; verifySign(x, hmac(x, k), k) = true.


(* Constants *)
const CreateSessionRequest: bitstring.
const CreateSessionResponse: bitstring.
const ActivateSessionRequest: bitstring.
const ActivateSessionResponse: bitstring.


(* Private keys *)
free skA: bitstring [private].
free skB: bitstring [private].


(* Secrets *)
fun Login(bitstring): bitstring.
fun Passwd(bitstring, bitstring): bitstring.
reduc forall x: bitstring, y: bitstring ; verifyCreds(x, Login(pub(y)), Passwd(y, pub(x))) = true.


(* Events *)
event beginCli(host, host, bitstring).
event endSrv(host, host, bitstring).
event beginSrv(host, host, bitstring).
event endCli(host, host, bitstring).


(* Goals *)
query attacker(Passwd(skA, pub(skB))).

query X: host, Y: host, N: bitstring ; event(endSrv(X, Y, N)) ==> event(beginCli(X, Y, N)).
query X: host, Y: host, N: bitstring ; event(endCli(X, Y, N)) ==> event(beginSrv(X, Y, N)).


let client(skA: bitstring, skB: bitstring, ch: channel) = 
    in(c, (Cli: host, Sess: host));
    new K: bitstring;
    out(ch, (Cli, Sess, K));
    let skCli = if Cli = A then skA else if Cli = B then skB in
    let pkCli = pub(skCli) in
    let skSess = if Sess = A then skA else if Sess = B then skB else sk(Sess) in
    let pkSess = pub(skSess) in
    let tmp = if Sess <> A && Sess <> B then K else CreateSessionRequest in out(c, tmp);

    (* Role starting *)
    new Nc: bitstring;
    let m1 = (CreateSessionRequest, pkCli, Nc) in
    out(c, (senc(m1, K), hmac(m1, K)));

    in(c, (em2: bitstring, sigM2: bitstring));
    let m2 = sdec(em2, K) in
    if verifySign(m2, sigM2, K) <> true then 0 else
    let (=CreateSessionResponse, =pkSess, =Nc, Ns: bitstring) = m2 in

    event beginCli(Cli, Sess, (Login(pkCli), Passwd(skCli, pkSess)));
    let m3 = (ActivateSessionRequest, pkCli, Login(pkCli), Passwd(skCli, pkSess)) in
    out(c, (senc(m3, K), hmac(m3, K)));

    in(c, (em4: bitstring, sigM4: bitstring));
    let m4 = sdec(em4, K) in
    if verifySign(m4, sigM4, K) <> true then 0 else
    let (=ActivateSessionResponse, Ns2: bitstring) = m4 in
    if Sess = A || Sess = B then
    event endCli(Sess, Cli, Nc).


let sess(skA: bitstring, skB: bitstring, ch: channel) = 
    in(ch, (Cli: host, Sess: host, K: bitstring));
    let skCli = if Cli = A then skA else if Cli = B then skB else sk(Cli) in
    let pkCli = pub(skCli) in
    let skSess = if Sess = A then skA else if Sess = B then skB in
    let pkSess = pub(skSess) in
    let tmp = if Cli <> A && Cli <> B then K else CreateSessionRequest in out(c, tmp);

    (* Role starting *)
    in(c, (em1: bitstring, sigM1: bitstring));
    new Nc: bitstring;
    let m1 = sdec(em1, K) in
    if verifySign(m1, sigM1, K) <> true then 0 else
    let (=CreateSessionRequest, =pkCli, Nc: bitstring) = m1 in

    new Ns: bitstring;
    event beginSrv(Sess, Cli, Nc);
    let m2 = (CreateSessionResponse, pkSess, Nc, Ns) in
    out(c, (senc(m2, K), hmac(m2, K)));

    in(c, (em3: bitstring, sigM3: bitstring));
    let m3 = sdec(em3, K) in
    if verifySign(m3, sigM3, K) <> true then 0 else
    let (=ActivateSessionRequest, =pkCli, login: bitstring, passwd: bitstring) = m3 in
    if verifyCreds(skSess, login, passwd) <> true then 0 else

    new Ns2: bitstring;
    let m4 = (ActivateSessionResponse, Ns2) in
    out(c, (senc(m4, K), hmac(m4, K)));
    if Cli = A || Cli = B then
    event endSrv(Cli, Sess, (login, passwd)).



process
    let pkA = pub(skA) in out(c, pkA);
    let pkB = pub(skB) in out(c, pkB);

    !(new ch: channel;
      client(skA, skB, ch)  |
      sess(skA, skB, ch)
    )
